// Domain : Temporal Machine Shop, Time (Temporally Expressive)

type Piece with{
	predicate baking();
	predicate treated();
	predicate baked();
};

type Kiln with{
	predicate ready();
	predicate empty();
};

variable boolean energy;
predicate structured(Piece p1, Piece p2);
predicate bakedStructure(Piece p1, Piece p2);


constant integer fireTime(Kiln k);
constant integer bakeTime(Piece p);
constant integer treatTime(Piece p);

action fire(Kiln k){
	duration := fireTime(k);
  	[all] energy == true;
        [start,start+1] k.ready == false :-> true ;
        [start+1,end-1] k.ready == true; 
	[end-1,end] k.ready == true :-> false;
};

action bake(Kiln k, Piece p){
	duration := bakeTime(p);
	[all] k.ready == true;
	[all] k.empty == true :-> true;
  	[start] p.baking := true ;
	[start+1,end-1] p.baking == true;
	[end-1,end] {
       	 p.baking == true :-> false ;
       	 p.baked == false :-> true ;
	};
};

action treat(Piece p){
	duration := treatTime(p);
	[all] p.baking == true;
	[end-1,end] p.treated == false :-> true;
};

action makeStructure(Piece p1, Piece p2){
	p1!=p2;
 	duration := 3;
	[all] {
		p1.baked == true;
		p2.baked == true;
		p1.treated == true;
		p2.treated == true;
	};
	[end-1,end] structured(p1, p2) == false :-> true;
};

action bakeStructure(Piece p1, Piece p2, Kiln k){
	p1!=p2;
	duration := 10;
	[all] k.ready == true;
	[all] structured(p1, p2) == true;
	[all] k.empty == true :-> true;
	[end-1,end] bakedStructure(p1,p2) == false :-> true;
};
