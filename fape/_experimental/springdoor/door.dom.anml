// -*-c++-*-
// the turn&open door domain for spring-doors which close when not held
// involve communication between robots, request and help
// simple version: one arm robots, implicit knob grasping actions, no space constraints

//*** types ***

type places ;

type rooms < places;

type objects_doors ;

type objects < objects_doors with {
  variable rooms pos; };

type doors < objects_doors with {
  predicate unlatched (); 
  predicate open(); };

type robots with {
  variable places loc;
  variable objects_doors grasp;
  predicate busy();};


constant boolean adjacent(places from, places tp);
constant boolean connect(doors d, places from, places to) ;


//constant boolean path(places from, places next, place to) ;
// path between places either adjacent or connected with doors
// needs to be generated from an all-shortest-path preprocessing.

instance objects_doors empty ; //denotes an empty handed robot
instance rooms held; //denotes the position of an object held by a robot


//*** primitives ***

action pickup (robots r, objects o, rooms p) {
  motivated ;
  [all] {
    r.loc == p;
    r.grasp == empty :-> o ;
    o.pos == p :-> held ;} ;
};

action drop (robots r, objects o, rooms p) {
  motivated ;
  [all] {
    r.loc == p;
    r.grasp == o :-> empty ;
    o.pos == held :-> p ;} ;
};

// elementary move from a room to adjacent door or the opposite
action move (robots r, places from, places to) {
  motivated ;
  adjacent(from, to) ;
  [all] r.loc == from :-> to; 
};

action turnknob (robots r, doors d) {
  motivated ;
  constant places from ;
  constant places to ;
  connect(d, from, to);
  [all] {
    r.loc == from;
    r.grasp == empty :-> d ;
    d.open == false ;
    d.unlatched == false :-> true ;};
};

action revolvehold (robots r, doors d) {
  motivated ;
  constant places from ;
  constant places to ;
  connect(d, from, to);
  [start] r.loc == from ;
  [all] {
    r.grasp == d :->  empty ;
    d.unlatched == true :-> false ;};
  [start, start+1] d.open == false :-> true ;
  [start+1, end] d.open == true ;
  [end + 1] d.open := false ;
  //[all] d.open == false :-> true ;
};

action passthru (robots r, doors d) {
  motivated ;
  constant places from ;
  constant places to ;
  connect(d, from, to);
  [all]{
    d.open == true ;
    r.loc == from :-> to ;};
};


//*** tasks ***

action opendoor (robots r, doors d) {
  motivated ;
  constant places from ;
  constant places to ;
  connect(d, from, to);
  :decomposition {
    [all] r.loc == from ;
    [start] r.grasp == empty ;
    [all] contains {
    s1 : turnknob(r, d) ;
    s2 : revolvehold(r, d) ;};
    end(s1) <= start(s2);
  };
};

action openpassdoor (robots r, doors d) {
  motivated ;
  constant places from ;
  constant places to ;
  connect(d, from, to);
  :decomposition {
    [start] {
      r.grasp == empty ;
      r.loc == from ;};
    [all] contains {
    s1 : turnknob(r, d) ;
    s2 : revolvehold(r, d);
    s3: passthru(r, d);};
    end(s1) <= start(s2);
    start(s2) <= start(s3);
    end(s3) <= end(s2);
  };
};


// r helps another robot
action gethelp (robots r, doors d) {
  motivated ;
  constant places from; // close to d ?
  constant places to;
  [start] r.loc==from ;
  :decomposition {
    connect(d, from, to );
    [start, start+1] r.busy==false :-> true ;
    [start +1 , end-1] r.busy== true ;
    [end] r.busy := false;
    [all] opendoor(r, d) ;
  };
  :decomposition {
    constant places nextp ;
    connect(d, to, nextp);
    from !=to;
    [start, start+1] r.busy==false :-> true ;
    [start +1 , end-1] r.busy== true ;
    [end] r.busy := false;
    [all] contains {
    s1: goto(r, from, to);
    s2: opendoor(r, d) ;};
    end(s1) <= start(s2);
  };
};

action gothrough(robots r, places from, places to, doors d) {
  motivated;
  connect(d, from, to);
  :decomposition { // r empty handed
    [start] r.grasp == empty;
    [all] openpassdoor(r,d);
  };
  :decomposition { // r not empty handed
    constant robots helper;
    [start] r.grasp != empty;
    [start] helper.busy == false;
    [all] h: gethelp(helper,d);
    [start(h),end(h)] contains passthru(r,d);
  };
};

// additional fluent to make sure the goto method terminates
fluent boolean visited(robots r, places p);
forall(robots r, places p) { [start] visited(r, p) := false; };

action goto (robots r, places from, places to) {
  motivated ;
  [start] r.loc == from ;

  // those make sure we do not visit the same place twice with the same goto
  [all] visited(r,from) == false :-> false;
  
  :decomposition {
    from == to;
  };
  :decomposition {
    constant places next ;
    adjacent(from, next) ;
    [all] ordered( 
		  move(r, from, next),
		  goto(r, next, to));
  };
  :decomposition {
    constant places next ;
    constant doors d;
    connect(d, from, next) ;
    [all] ordered(
		  gothrough(r, from, next, d),
		  goto(r, next, to));
  };
};

  
action fetch (objects o, rooms p) {
  motivated ;
  constant robots r ;
  constant places from ;
  :decomposition {
    [start] o.pos == p ;
  };
  :decomposition {
    [start] {
      o.pos == held ;
      r.grasp == o ;
      r.loc == from ;};
    [all] contains {
    s1: goto(r, from, p) ;
    s2: drop(r, o, p) ;};
    end(s1) <= start(s2) ;
  };
  :decomposition{
    constant rooms roominit;
    [start] {
      o.pos == roominit ;
      r.loc == from ;};
    //    [all] r.busy==false :=true := false ;
    [start, start+1] r.busy==false :-> true ;
    [start +1 , end-1] r.busy== true ;
    [end] r.busy := false;
    roominit != p ;
    [all] contains {
    s1: goto(r, from, roominit);
    s2: pickup(r,o, roominit);
    s3: goto(r, roominit, p);
    s4: drop(r, o, p);};
    end(s1) <= start(s2);
    end(s2) <= start(s3);
    end(s3) <= start(s4);
  };
};


/*  Draft
    action waittoopen(robots r, doors d) {
    connect(doors d, places p1, places p2);
    [all] r.loc == p1;
    [start] d.open == false;
    [end] d.open == true ;
    };
    in goto
    :decomposition {
    constant doors d;
    connect(d, from, to);
    [start] r.grasp != empty ;
    [start] d.helprequested == true ; //not sure this case arrises ?
    [all] contains {
    s1 : waittoopen(r, d);
    s2 : passthru(r, d);}
    end(s1) <= start(s2);
    [end] d.helprequested := false ;
    };*/
