
type Location; 
type Block < Location;

predicate clear(Block b);
predicate handempty();
function Location on(Block b);

instance Location TABLE, HAND;


action pickup(Block b) {
  motivated;
  duration := 5;
  [all] clear(b) == true;
  [all] on(b) == TABLE :-> HAND;
  [all] handempty == true :-> false;
};

action putdown(Block b) {
  motivated;
  duration := 5;  
  [all] clear(b) == true;
  [all] on(b) == HAND :-> TABLE;
  [all] handempty == false :-> true;
};

action stack(Block b, Block c) {
  motivated;
  duration := 5;  
  [all] on(b) == HAND :-> c;
  [all] handempty == false :-> true;
  [all] clear(c) == true :-> false;
  [all] clear(b) == true;
};

action unstack(Block b, Block c) {
  motivated;
  duration := 5;  
  [all] on(b) == c :-> HAND;
  [all] handempty == true :-> false;
  [all] clear(b) == true;
  [all] clear(c) == false :-> true;
};

action uncover(Block a) {
  motivated;
  :decomposition {
    [all] clear(a) == true;
  };
  :decomposition {
    [start] clear(a) == false;
    constant Block onA;
    [start] on(onA) == a;
    [all] ordered(
      uncover(onA),
      unstack(onA,a),
      putdown(onA));
  };
};

action DoStack(Block a, Block b) {
  motivated;
  :decomposition {
    [all] on(a) == b;
  };
  :decomposition {
    [start] on(a) == TABLE;
    [all] ordered(
      uncover(a),
      uncover(b),
      p: pickup(a),
      s: stack(a, b));
      end(p) = start(s);
  };
  :decomposition {
    constant Block other;
    other != TABLE;
    [start] on(a) == other;
    [all] ordered(
      uncover(a),
      uncover(b),
      u: unstack(a, other),
      s: stack(a, b));
    end(u) = start(s);

  };
};
